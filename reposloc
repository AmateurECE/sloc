#!/usr/bin/perl

use strict;
use warnings;

use Cwd qw/getcwd/;
use DateTime;
use File::Path qw/rmtree/;
use JSON qw/from_json/;
use GD::Graph;
use GD::Graph::area;

sub format_date {
	my $dt = DateTime->from_epoch(epoch => shift);
	return $dt->datetime();
}

my $calldir = getcwd;
my $repo = shift || '.';
my $destrepo = '/tmp/reposloc-' . int(rand(100000));

system "git", "clone", "-q", $repo, $destrepo;
chdir $destrepo or die $!;

my @rdata;

open IN, 'git rev-list master --reverse --timestamp |' or die $!;
while (<IN>) {
	chomp;
	my ($ts, $rev) = split / /;
	system "git checkout --quiet $rev";
	my $json = `sloc -json`;
	my $result = from_json $json or die $!;

	my ($filecount, $total, $code, $blank, $comment) = (0, 0, 0, 0, 0);

	for my $lang (keys $result) {
		$filecount += $result->{$lang}{"FileCount"};
		$total += $result->{$lang}{TotalLines};
		$code += $result->{$lang}{CodeLines};
		$blank += $result->{$lang}{BlankLines};
		$comment += $result->{$lang}{CommentLines};
	}

	push @rdata, [$ts, $code, $blank, $comment];
}
close IN or die $!;

my @data;
for my $y (0 .. $#rdata) {
	for my $x (0 .. $#{$rdata[$y]}) {
		$data[$x][$y] = $rdata[$y][$x];
	}
}

my $gr = GD::Graph::area->new(1440, 900);
$gr->set(
	transparent => 0,
	cumulate => 1,
	x_tick_number => 'auto',
	x_labels_vertical => 1,
	x_number_format => \&format_date,
);
$gr->set_legend("Code", "Blank", "Comment");
$gr->plot(\@data);

chdir $calldir;
open OUT, ">out.png" or die $!;
binmode OUT;
print OUT $gr->gd->png();
close OUT or die $!;

rmtree $destrepo;

